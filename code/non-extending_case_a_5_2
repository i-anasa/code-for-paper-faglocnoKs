# GAP code, see https://www.gap-system.org/
# Finite quotient of an NEC group with signature (0;+;[5];{(2)})
# acting with said signature as the full group Aut(S)
# on a non-orientable surface S.

# generators of G
a := (1,20,16,12,8)(2,17,13,9,5)(3,18,14,10,6)(4,19,15,11,7);;
c := (3,4)(9,10)(11,12)(15,16);;

# finite group acting with signature sigma
G := Group([a, c]);;
Print (
"\n  Properties of G:\n-------------------------------------",
    "\n Size: ", Size(G),
    "\n Orders of the elements: \n ", 
    " o(a) = ", Order(a), " | ",
    " o(c) = ", Order(c), " | ",
    " o([a,c]]) = ", Order(a*c*a^-1*c), " | ",
    " o(ac) = ", Order(a*c), "\n "
);

# Try to define the target map h : a |-> a^(-1), c |-> c.
Print("\n Map h: a |-> a^(-1), c |-> c :\n");
h := GroupGeneralMappingByImages( G, G, [a, c], [a^(-1), c] );;

# Check whether it is a valid map.
if IsMapping(h) then
    # Check whether it is injective (ker T = {1}).
    if Size(Kernel(h)) = 1 then
        Print("   ... is a bijective homomorphism. \n");
    fi;
# Not a valid map.
else
    Print("   ... is not a valid homomorphism. \n");
    Print(" Images of identity: ", Size(ImagesElm( h, One(G))), " \n");
fi;

