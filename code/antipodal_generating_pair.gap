# GAP code, see https://www.gap-system.org/
# Checks if a pair of permutations form an
# antipodal generating set for a
# group of automorphisms acting on a non-orientable
# surface with signature (0;+;[-];{(2,m,n)}).

# Input data ###################
# periods.
m := 3 ;; n := 12;;

# Of order 2.
x := (1, 7)(2, 6)(3, 4);;

# Of order m.
y := (1, 2, 3)(4, 5, 6);;

# Mirror reflection.
b := (2,3)(4,6);;

# Verification ###################

# Check orders.
ox := Order(x);;
oy := Order(y);;
oxy := Order(x*y);;

# Group generated by X and Y. Basic info.
G := Group([x, y]);;
Print(" Structure of G = <x,y> = ", StructureDescription(G), ".\n");
Print(" |G| = ", Size(G), ".\n");;
Print(" G Is transitive: ", IsTransitive(G), ".\n");
Print(" G Is primitive: ", IsPrimitive(G), ".\n");

# Check if the mirror reflection b is in G.
if b in G then
   Print(" G contains the mirror reflection b. {x,y} is an antipodal pair.\n");
   if ox = 2 and oy = m and oxy = n then
     Print(" G acts with signature (0;+;[-];{(2,",m,",",n,")}) under the epimorphism:\n");
     Print("    a -> ", x*b, "\n");
     Print("    b -> ", b, "\n");
     Print("    c -> ", b*y, "\n");
   else
     Print(" G acts with the wrong signature: (0;+;[-];{(2,",oy,",",oxy,")}).\n");
   fi;
else  
   Print(" G does not contain the mirror reflection b. {x,y} is not an antipodal pair.\n");
fi;;
